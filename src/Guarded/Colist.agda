{-# OPTIONS --guarded #-}
module Guarded.Colist where

open import Prelude
open import Data.Unit
open import Data.Empty
open import Data.Maybe
open import Data.Nat
open import Data.List

open import LaterG

private variable
  â„“ â„“â€² â„“â€³ : Level
  A : ğ’° â„“
  B : ğ’° â„“â€²
  C : ğ’° â„“â€³

-- guarded colists

data Colist (A : ğ’° â„“) : ğ’° â„“ where
  cnil  : Colist A
  ccons : A â†’ â–¹ Colist A â†’ Colist A

module Colist-code where
  Code-body : â–¹ (Colist A â†’ Colist A â†’ ğ’° (level-of-type A)) â†’ Colist A â†’ Colist A â†’ ğ’° (level-of-type A)
  Code-body Câ–¹  cnil          cnil         = Lift _ âŠ¤
  Code-body Câ–¹  cnil         (ccons _ _)   = Lift _ âŠ¥
  Code-body Câ–¹ (ccons _ _)    cnil         = Lift _ âŠ¥
  Code-body Câ–¹ (ccons x cxâ–¹) (ccons y cyâ–¹) = (x ï¼ y) Ã— (â–¸ (Câ–¹ âŠ› cxâ–¹ âŠ› cyâ–¹))

  Code : Colist A â†’ Colist A â†’ ğ’° (level-of-type A)
  Code = fix Code-body

  Code-cc-eq : {x y : A} {cxâ–¹ cyâ–¹ : â–¹ Colist A}
             â†’ Code (ccons x cxâ–¹) (ccons y cyâ–¹) ï¼ (x ï¼ y) Ã— â–¸ (Code â‰ cxâ–¹ âŠ› cyâ–¹)
  Code-cc-eq {x} {y} {cxâ–¹} {cyâ–¹} i = (x ï¼ y) Ã— (â–¹[ Î± ] pfix Code-body i Î± (cxâ–¹ Î±) (cyâ–¹ Î±))

  Code-ccâ‡‰ : {x y : A} {cxâ–¹ cyâ–¹ : â–¹ Colist A}
           â†’ Code (ccons x cxâ–¹) (ccons y cyâ–¹)
           â†’ (x ï¼ y) Ã— â–¸ (Code â‰ cxâ–¹ âŠ› cyâ–¹)
  Code-ccâ‡‰ = transport Code-cc-eq

  â‡‰Code-cc : {x y : A} {cxâ–¹ cyâ–¹ : â–¹ Colist A}
           â†’ (x ï¼ y) Ã— â–¸ (Code â‰ cxâ–¹ âŠ› cyâ–¹)
           â†’ Code (ccons x cxâ–¹) (ccons y cyâ–¹)
  â‡‰Code-cc = transport (sym Code-cc-eq)

  Code-refl-body : â–¹ ((c : Colist A) â†’ Code c c) â†’ (c : Colist A) â†’ Code c c
  Code-refl-body Câ–¹  cnil        = lift tt
  Code-refl-body Câ–¹ (ccons x câ–¹) = â‡‰Code-cc (refl , (Câ–¹ âŠ› câ–¹))

  Code-refl : (c : Colist A) â†’ Code c c
  Code-refl = fix Code-refl-body

  encode : {c1 c2 : Colist A} â†’ c1 ï¼ c2 â†’ Code c1 c2
  encode {c1} {c2} e = subst (Code c1) e (Code-refl c1)

  decode : (m n : Colist A) â†’ Code m n â†’ m ï¼ n
  decode  cnil          cnil          c = refl
  decode (ccons x cxâ–¹) (ccons y cyâ–¹)  c =
    let (ex , ec) = Code-ccâ‡‰ c in
    apÂ² ccons ex (â–¹-ext Î» Î± â†’ decode (cxâ–¹ Î±) (cyâ–¹ Î±) (ec Î±))

cnilâ‰ ccons : {x : A} {câ–¹ : â–¹ Colist A} â†’ cnil â‰  ccons x câ–¹
cnilâ‰ ccons = lower âˆ˜ Colist-code.encode

ccons-inj : {x y : A} {cxâ–¹ cyâ–¹ : â–¹ Colist A}
          â†’ ccons x cxâ–¹ ï¼ ccons y cyâ–¹ â†’ (x ï¼ y) Ã— (cxâ–¹ ï¼ cyâ–¹)
ccons-inj {x} {y} {cxâ–¹} {cyâ–¹} e =
  let (ex , ec) = Colist-code.Code-ccâ‡‰ (Colist-code.encode e) in
  ex , â–¹-ext (Colist-code.decode â‰ cxâ–¹ âŠ›â–¹ cyâ–¹ âŠ›â–¹ ec)

prepend : A â†’ Colist A â†’ Colist A
prepend a = ccons a âˆ˜ next

-- singleton

singletonË¡ : A â†’ Colist A
singletonË¡ a = prepend a cnil

-- repeat

repeatË¡ : A â†’ Colist A
repeatË¡ = fix âˆ˜ ccons

-- uncons

unconsË¡ : Colist A -> Maybe (A Ã— â–¹ Colist A)
unconsË¡  cnil         = nothing
unconsË¡ (ccons a asâ–¹) = just (a , asâ–¹)

-- fromList

fromList : List A â†’ Colist A
fromList []      = cnil
fromList (x âˆ· l) = prepend x (fromList l)

-- catList

catList : List A â†’ Colist A â†’ Colist A
catList []      c = c
catList (x âˆ· l) c = prepend x (catList l c)

catFromList : (as bs : List A)
            â†’ fromList (as ++ bs) ï¼ catList as (fromList bs)
catFromList []       bs = refl
catFromList (a âˆ· as) bs = ap (prepend a) (catFromList as bs)

-- append

appendË¡-body : â–¹ (Colist A â†’ Colist A â†’ Colist A) â†’ Colist A â†’ Colist A â†’ Colist A
appendË¡-body apâ–¹  cnil         t = t
appendË¡-body apâ–¹ (ccons x xsâ–¹) t = ccons x ((apâ–¹ âŠ› xsâ–¹) âŠ› next t)

appendË¡ : Colist A â†’ Colist A â†’ Colist A
appendË¡ = fix appendË¡-body

-- map

mapË¡-body : (A â†’ B) â†’ â–¹ (Colist A â†’ Colist B) â†’ Colist A â†’ Colist B
mapË¡-body f mpâ–¹  cnil         = cnil
mapË¡-body f mpâ–¹ (ccons x xsâ–¹) = ccons (f x) (mpâ–¹ âŠ› xsâ–¹)

mapË¡ : (A â†’ B) â†’ Colist A â†’ Colist B
mapË¡ f = fix (mapË¡-body f)

map-catList : âˆ€ {A : ğ’° â„“} {B : ğ’° â„“â€²}
            â†’ (f : A â†’ B) â†’ (l : List A) â†’ (c : Colist A)
            â†’ mapË¡ f (catList l c) ï¼ catList (map f l) (mapË¡ f c)
map-catList f []      c = refl
map-catList f (x âˆ· l) c = ap (ccons (f x)) (â–¹-ext Î» Î± â†’ (Î» i â†’ pfix (mapË¡-body f) i Î± (catList l c))
                                                             âˆ™ map-catList f l c)

-- zipWith

zipWithË¡-body : (A â†’ B â†’ C) â†’ â–¹ (Colist A â†’ Colist B â†’ Colist C) â†’ Colist A â†’ Colist B â†’ Colist C
zipWithË¡-body f zwâ–¹  cnil          cnil         = cnil
zipWithË¡-body f zwâ–¹  cnil         (ccons _ _  ) = cnil
zipWithË¡-body f zwâ–¹ (ccons _ _  )  cnil         = cnil
zipWithË¡-body f zwâ–¹ (ccons a asâ–¹) (ccons b bsâ–¹) = ccons (f a b) (zwâ–¹ âŠ› asâ–¹ âŠ› bsâ–¹)

zipWithË¡ : (A â†’ B â†’ C) â†’ Colist A â†’ Colist B â†’ Colist C
zipWithË¡ f = fix (zipWithË¡-body f)

-- intersperse

prepend-to-allË¡-body : A â†’ â–¹ (Colist A â†’ Colist A) â†’ Colist A â†’ Colist A
prepend-to-allË¡-body sep pâ–¹  cnil         = cnil
prepend-to-allË¡-body sep pâ–¹ (ccons x xsâ–¹) = prepend sep (ccons x (pâ–¹ âŠ› xsâ–¹))

prepend-to-allË¡ : A â†’ Colist A â†’ Colist A
prepend-to-allË¡ sep = fix (prepend-to-allË¡-body sep)

intersperseË¡ : A â†’ Colist A â†’ Colist A
intersperseË¡ sep  cnil         = cnil
intersperseË¡ sep (ccons x xsâ–¹) = ccons x ((prepend-to-allË¡ sep) â‰ xsâ–¹)

-- foldr

foldrË¡-body : (A â†’ â–¹ B â†’ B) â†’ B â†’ â–¹ (Colist A â†’ B) â†’ Colist A â†’ B
foldrË¡-body f z fâ–¹  cnil         = z
foldrË¡-body f z fâ–¹ (ccons x xsâ–¹) = f x (fâ–¹ âŠ› xsâ–¹)

foldrË¡ : (A â†’ â–¹ B â†’ B) â†’ Colist A â†’ B â†’ B
foldrË¡ f c z = fix (foldrË¡-body f z) c

-- finiteness

is-finiteË¡ : Colist A â†’ ğ’° (level-of-type A)
is-finiteË¡ = fibre fromList

is-finite-uncons : (x : A) (câ–¹ : â–¹ Colist A) â†’ is-finiteË¡ (ccons x câ–¹) â†’ â–¸ (is-finiteË¡ â‰ câ–¹)
is-finite-uncons x câ–¹ ([]    , e) = absurd (cnilâ‰ ccons e)
is-finite-uncons x câ–¹ (y âˆ· l , e) = Î» Î± â†’ l , (â–¹-ap (ccons-inj e .snd) Î±)

is-finite-downË¡ : (x : A) (c : Colist A) â†’ is-finiteË¡ (prepend x c) â†’ â–¹ (is-finiteË¡ c)
is-finite-downË¡ x c = is-finite-uncons x (next c)

is-finite-upË¡ : (x : A) (c : Colist A) â†’ is-finiteË¡ c â†’ is-finiteË¡ (prepend x c)
is-finite-upË¡ x c (l , e) = (x âˆ· l) , ap (prepend x) e

-- propositional version

is-finite-pË¡ : Colist A â†’ ğ’° (level-of-type A)
is-finite-pË¡ = âˆ¥_âˆ¥â‚ âˆ˜ is-finiteË¡

is-finite-uncons-p : (x : A) (câ–¹ : â–¹ Colist A) â†’ is-finite-pË¡ (ccons x câ–¹) â†’ â–¸ (is-finite-pË¡ â‰ câ–¹)
is-finite-uncons-p x câ–¹ p = â–¹truncâ‚ id (map (is-finite-uncons x câ–¹) p)

is-finite-down-pË¡ : (x : A) (c : Colist A) â†’ is-finite-pË¡ (prepend x c) â†’ â–¹ (is-finite-pË¡ c)
is-finite-down-pË¡ x c = is-finite-uncons-p x (next c)

is-finite-up-pË¡ : (x : A) (c : Colist A) â†’ is-finite-pË¡ c â†’ is-finite-pË¡ (prepend x c)
is-finite-up-pË¡ x c = map (is-finite-upË¡ x c)
