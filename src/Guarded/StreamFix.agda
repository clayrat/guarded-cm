{-# OPTIONS --guarded #-}
module Guarded.StreamFix where

open import Prelude
open import Foundations.Transport
open import Data.Bool
open import Data.Nat
open import Data.List

open import LaterG

private variable
  A B C : ğ’°

-- guarded streams via fixpoint in the universe

StreamF : ğ’° â†’ â–¹ ğ’° â†’ ğ’°
StreamF A Sâ–¹ = A Ã— â–¸ Sâ–¹

Stream : ğ’° â†’ ğ’°
Stream A = fix (StreamF A)

opaque
  Stream-path : Stream A ï¼ StreamF A (next (Stream A))
  Stream-path {A} = fix-path (StreamF A)

  Streamâ‡‰ : Stream A
           â†’ StreamF A (next (Stream A))
  Streamâ‡‰ = transport Stream-path

  â‡‰Stream : StreamF A (next (Stream A))
           â†’ Stream A
  â‡‰Stream = transport (Stream-path â»Â¹)

consË¢ : A â†’ â–¹ Stream A â†’ Stream A
consË¢ x xsâ–¹ = â‡‰Stream (x , xsâ–¹)

unconsË¢ : Stream A â†’ A Ã— â–¹ Stream A
unconsË¢ = Streamâ‡‰

headË¢ : Stream A â†’ A
headË¢ = fst âˆ˜ unconsË¢

tailâ–¹Ë¢ : Stream A â†’ â–¹ Stream A
tailâ–¹Ë¢ = snd âˆ˜ unconsË¢

opaque
  unfolding Streamâ‡‰ â‡‰Stream
  
  uncons-eq : (s : Stream A) â†’ s ï¼ consË¢ (headË¢ s) (tailâ–¹Ë¢ s)
  uncons-eq {A} s = transportâ»-transport Stream-path s â»Â¹

  head-cons : (a : A) (asâ–¹ : â–¹ Stream A) â†’ headË¢ (consË¢ a asâ–¹) ï¼ a
  head-cons {A} a asâ–¹ = transportâ»-transport refl a 

  tail-cons : (a : A) (asâ–¹ : â–¹ Stream A) â†’ tailâ–¹Ë¢ (consË¢ a asâ–¹) ï¼ asâ–¹
  tail-cons {A} a asâ–¹ = transportâ»-transport (Î» i â†’ â–¸ pfix (StreamF A) (~ i)) asâ–¹

-- repeat

repeatË¢ : A â†’ Stream A
repeatË¢ a = fix (consË¢ a)

repeatË¢-eq : (a : A) â†’ repeatË¢ a ï¼ consË¢ a (Î» Î± â†’ repeatË¢ a)
repeatË¢-eq a = ap (consË¢ a) (pfix (consË¢ a))

-- map

mapË¢-body : (A â†’ B) â†’ â–¹ (Stream A â†’ Stream B) â†’ Stream A â†’ Stream B
mapË¢-body f mâ–¹ as = consË¢ (f (headË¢ as)) (mâ–¹ âŠ› (tailâ–¹Ë¢ as))

mapË¢ : (A â†’ B) â†’ Stream A â†’ Stream B
mapË¢ f = fix (mapË¢-body f)

mapË¢-eq : (f : A â†’ B) â†’ (a : A) â†’ (asâ–¹ : â–¹ Stream A)
        â†’ mapË¢ f (consË¢ a asâ–¹) ï¼ consË¢ (f a) ((mapË¢ f) â‰ asâ–¹)
mapË¢-eq {A} f a asâ–¹ =
    happly (fix-path (mapË¢-body f)) (consË¢ a asâ–¹)
  âˆ™ apÂ² consË¢
      (ap f (head-cons a asâ–¹))
      (ap (mapË¢ f â‰_) (tail-cons a asâ–¹))

mapË¢-head : (f : A â†’ B) â†’ (s : Stream A)
          â†’ headË¢ (mapË¢ f s) ï¼ f (headË¢ s)
mapË¢-head f s =
    ap headË¢ (happly (fix-path (mapË¢-body f)) s) 
  âˆ™ head-cons (f (headË¢ s)) ((mapË¢ f) â‰ (tailâ–¹Ë¢ s))

mapË¢-tail : (f : A â†’ B) â†’ (s : Stream A)
          â†’ tailâ–¹Ë¢ (mapË¢ f s) ï¼ (mapË¢ f) â‰ (tailâ–¹Ë¢ s)
mapË¢-tail f s =
    ap (Î» q â†’ tailâ–¹Ë¢ (mapË¢ f q)) (uncons-eq s)
  âˆ™ ap tailâ–¹Ë¢ (mapË¢-eq f (headË¢ s) (tailâ–¹Ë¢ s))
  âˆ™ tail-cons (f (headË¢ s)) ((mapË¢ f) â‰ (tailâ–¹Ë¢ s))

mapË¢-fusion : (f : A â†’ B) â†’ (g : B â†’ C) â†’ (s : Stream A)
            â†’ mapË¢ g (mapË¢ f s) ï¼ mapË¢ (g âˆ˜ f) s
mapË¢-fusion f g =
  fix Î» prfâ–¹ s â†’ let (a , asâ–¹) = unconsË¢ s in
    mapË¢ g (mapË¢ f s)
      =âŸ¨ ap (mapË¢ g âˆ˜ mapË¢ f) (uncons-eq s) âŸ©
    mapË¢ g (mapË¢ f (consË¢ a asâ–¹))
      =âŸ¨ ap (mapË¢ g) (mapË¢-eq f a asâ–¹) âŸ©
    mapË¢ g (consË¢ (f a) ((mapË¢ f) â‰ asâ–¹))
      =âŸ¨ mapË¢-eq g (f a) ((mapË¢ f) â‰ asâ–¹) âŸ©
    consË¢ (g (f a)) ((mapË¢ g) â‰ ((mapË¢ f) â‰ asâ–¹))
      =âŸ¨ ap (consË¢ (g (f a))) (â–¹-ext (prfâ–¹ âŠ› asâ–¹)) âŸ©
    consË¢ (g (f a)) ((mapË¢ (g âˆ˜ f)) â‰ asâ–¹)
      =âŸ¨ sym (mapË¢-eq (g âˆ˜ f) a asâ–¹) âŸ©
    mapË¢ (g âˆ˜ f) (consË¢ a asâ–¹)
      =âŸ¨ ap (mapË¢ (g âˆ˜ f)) (sym (uncons-eq s)) âŸ©
    mapË¢ (g âˆ˜ f) s
      âˆ

mapË¢-repeat : (a : A) â†’ (f : A â†’ B) â†’ mapË¢ f (repeatË¢ a) ï¼ repeatË¢ (f a)
mapË¢-repeat a f = fix Î» prfâ–¹ â†’
  mapË¢ f (repeatË¢ a)
    =âŸ¨ ap (mapË¢ f) (repeatË¢-eq a)  âŸ©
  mapË¢ f (consË¢ a (Î» Î± â†’ repeatË¢ a))
    =âŸ¨ mapË¢-eq f a (Î» x â†’ consË¢ a (dfix (consË¢ a))) âŸ©
  consË¢ (f a) (Î» Î± â†’ mapË¢ f (repeatË¢ a))
    =âŸ¨ ap (consË¢ (f a)) (â–¹-ext prfâ–¹) âŸ©
  consË¢ (f a) (Î» Î± â†’ repeatË¢ (f a))
    =âŸ¨ ap (consË¢ (f a)) (â–¹-ext Î» Î± â†’ sym (pfix-ext (consË¢ (f a)) Î±)) âŸ©
  consË¢ (f a) (Î» Î± â†’ dfix (consË¢ (f a)) Î±)
    =âŸ¨âŸ©
  repeatË¢ (f a)
    âˆ

-- lift a predicate to a stream

AllË¢-body : (A â†’ ğ’°) â†’ â–¹ (Stream A â†’ ğ’°) â†’ Stream A â†’ ğ’°
AllË¢-body P Pâ–¹ s = P (headË¢ s) Ã— â–¸ (Pâ–¹ âŠ› (tailâ–¹Ë¢ s))

AllË¢ : (A â†’ ğ’°) â†’ Stream A â†’ ğ’°
AllË¢ P = fix (AllË¢-body P)

AllË¢-cons : âˆ€ {a asâ–¹} {P : A â†’ ğ’°} â†’ P a â†’ â–¹[ Î± ] (AllË¢ P (asâ–¹ Î±)) â†’ AllË¢ P (consË¢ a asâ–¹)
AllË¢-cons {a} {asâ–¹} {P} pa psâ–¹ =
    subst P (head-cons a asâ–¹ â»Â¹) pa
  , (subst (Î» q â†’ â–¸ (dfix (AllË¢-body P) âŠ› q)) ((tail-cons a asâ–¹) â»Â¹) $
     subst (Î» q â†’ â–¸ (q âŠ› asâ–¹)) ((pfix (AllË¢-body P)) â»Â¹) $
     psâ–¹)

AllË¢-match : âˆ€ {a asâ–¹} {P : A â†’ ğ’°} â†’ AllË¢ P (consË¢ a asâ–¹) â†’ P a Ã— (â–¹[ Î± ] (AllË¢ P (asâ–¹ Î±)))
AllË¢-match {a} {asâ–¹} {P} (pa , psâ–¸) =
    subst P (head-cons a asâ–¹) pa
  , (subst (Î» q â†’ â–¸ (q âŠ› asâ–¹)) (pfix (AllË¢-body P)) $
     subst (Î» q â†’ â–¸ (dfix (AllË¢-body P) âŠ› q)) (tail-cons a asâ–¹) $
     psâ–¸)

AllË¢-map : {P Q : A â†’ ğ’°} {f : A â†’ A}
         â†’ ({x : A} â†’ P x â†’ Q (f x))
         â†’ (s : Stream A) â†’ AllË¢ P s â†’ AllË¢ Q (mapË¢ f s)
AllË¢-map {P} {Q} {f} pq =
  fix Î» prfâ–¹ s ps â†’
    let pa , pasâ–¹ = AllË¢-match {P = P} (subst (AllË¢ P) (uncons-eq s) ps) in
    subst (AllË¢ Q âˆ˜ mapË¢ f) ((uncons-eq s) â»Â¹) $
    subst (AllË¢ Q) ((mapË¢-eq f (headË¢ s) (tailâ–¹Ë¢ s)) â»Â¹) $
    AllË¢-cons {P = Q} (pq pa) ((Î» Î± â†’ prfâ–¹ Î± (tailâ–¹Ë¢ s Î±) (pasâ–¹ Î±)))
  
-- folding

foldrË¢-body : (A â†’ â–¹ B â†’ B) â†’ â–¹ (Stream A â†’ B) â†’ Stream A â†’ B
foldrË¢-body f fâ–¹ s = f (headË¢ s) (fâ–¹ âŠ› tailâ–¹Ë¢ s)

foldrË¢ : (A â†’ â–¹ B â†’ B) â†’ Stream A â†’ B
foldrË¢ f = fix (foldrË¢-body f)

scanl1Ë¢-body : (A â†’ A â†’ A) â†’ â–¹ (Stream A â†’ Stream A) â†’ Stream A â†’ Stream A
scanl1Ë¢-body f scâ–¹ s = consË¢ (headË¢ s) ((mapË¢ (f (headË¢ s))) â‰ (scâ–¹ âŠ› tailâ–¹Ë¢ s))

scanl1Ë¢ : (A â†’ A â†’ A) â†’ Stream A â†’ Stream A
scanl1Ë¢ f = fix (scanl1Ë¢-body f)

-- iterate

iterateË¢-body : â–¹ (A â†’ A) â†’ â–¹ (A â†’ Stream A) â†’ A â†’ Stream A
iterateË¢-body f iâ–¹ a = consË¢ a (iâ–¹ âŠ› (f âŠ› next a))

iterateË¢ : â–¹ (A â†’ A) â†’ A â†’ Stream A
iterateË¢ f = fix (iterateË¢-body f)

tail-iterateË¢ : (fâ–¹ : â–¹ (A â†’ A)) â†’ (x : A)
              â†’ tailâ–¹Ë¢ (iterateË¢ fâ–¹ x) ï¼ (iterateË¢ fâ–¹) â‰ (fâ–¹ âŠ› next x)
tail-iterateË¢ f x =
  tail-cons x (dfix (iterateË¢-body f) âŠ› (f âŠ› next x))
  âˆ™ ap (_âŠ› (f âŠ› next x)) (pfix (iterateË¢-body f))

-- interleave

interleaveË¢ : Stream A â†’ â–¹ Stream A â†’ Stream A
interleaveË¢ = fix Î» iâ–¹ s tâ–¹ â†’ consË¢ (headË¢ s) (iâ–¹ âŠ› tâ–¹ âŠ› next (tailâ–¹Ë¢ s))

-- zipping

zipWithË¢-body : (A â†’ B â†’ C) â†’ â–¹ (Stream A â†’ Stream B â†’ Stream C) â†’ Stream A â†’ Stream B â†’ Stream C
zipWithË¢-body f zwâ–¹ sa sb = consË¢ (f (headË¢ sa) (headË¢ sb)) (zwâ–¹ âŠ› tailâ–¹Ë¢ sa âŠ› tailâ–¹Ë¢ sb)

zipWithË¢ : (A â†’ B â†’ C) â†’ Stream A â†’ Stream B â†’ Stream C
zipWithË¢ f = fix (zipWithË¢-body f)

-- comonad structure

extractË¢ : Stream A â†’ A
extractË¢ = headË¢

-- aka tails
duplicateË¢-body : â–¹ (Stream A â†’ Stream (Stream A)) â†’ Stream A â†’ Stream (Stream A)
duplicateË¢-body dâ–¹ s = consË¢ s (dâ–¹ âŠ› tailâ–¹Ë¢ s)

duplicateË¢ : Stream A â†’ Stream (Stream A)
duplicateË¢ = fix duplicateË¢-body

extendË¢-body : (Stream A â†’ B) â†’ â–¹ (Stream A â†’ Stream B) â†’ Stream A â†’ Stream B
extendË¢-body f eâ–¹ s = consË¢ (f s) (eâ–¹ âŠ› tailâ–¹Ë¢ s)

extendË¢ : (Stream A â†’ B) â†’ Stream A â†’ Stream B
extendË¢ f = fix (extendË¢-body f)

extract-duplicate : (s : Stream A) â†’ extractË¢ (duplicateË¢ s) ï¼ s
extract-duplicate s =
  extractË¢ (duplicateË¢ s)
    =âŸ¨ ap (Î» q â†’ extractË¢ (q s)) (fix-path duplicateË¢-body) âŸ©
  extractË¢ (duplicateË¢-body (next duplicateË¢) s)
    =âŸ¨ head-cons s (duplicateË¢ â‰ tailâ–¹Ë¢ s) âŸ©
  s
    âˆ

map-extract-duplicate : (s : Stream A) â†’ mapË¢ extractË¢ (duplicateË¢ s) ï¼ s
map-extract-duplicate = fix Î» ihâ–¹ â†’ Î» where
  s â†’
    mapË¢ extractË¢ (duplicateË¢ s)
      =âŸ¨ ap (Î» q â†’ mapË¢ extractË¢ (q s)) (fix-path duplicateË¢-body) âŸ©
    mapË¢ extractË¢ (duplicateË¢-body (next duplicateË¢) s)
      =âŸ¨ mapË¢-eq extractË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s) âŸ©
    consË¢ (headË¢ s) (mapË¢ extractË¢ â‰ (duplicateË¢ â‰ tailâ–¹Ë¢ s))
      =âŸ¨ ap (consË¢ (headË¢ s)) (â–¹-ext (ihâ–¹ âŠ› tailâ–¹Ë¢ s)) âŸ©
    consË¢ (headË¢ s) (tailâ–¹Ë¢ s)
      =âŸ¨ uncons-eq s âŸ¨
    s
      âˆ

duplicate-duplicate : (s : Stream A) â†’ duplicateË¢ (duplicateË¢ s) ï¼ mapË¢ duplicateË¢ (duplicateË¢ s)
duplicate-duplicate = fix Î» ihâ–¹ â†’ Î» where
  s â†’
    duplicateË¢ (duplicateË¢ s)
      =âŸ¨ ap (Î» q â†’ duplicateË¢ (q s)) (fix-path duplicateË¢-body) âŸ©
    duplicateË¢ (duplicateË¢-body (next duplicateË¢) s)
      =âŸ¨ ap (Î» q â†’ q (duplicateË¢-body (next duplicateË¢) s)) (fix-path duplicateË¢-body) âŸ©
    duplicateË¢-body (next duplicateË¢) (duplicateË¢-body (next duplicateË¢) s)
      =âŸ¨âŸ©
    consË¢ (consË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s)) (duplicateË¢ â‰ âŒœ tailâ–¹Ë¢ (consË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s)) âŒ)
      =âŸ¨ ap! (tail-cons s (duplicateË¢ â‰ tailâ–¹Ë¢ s)) âŸ©
    consË¢ (consË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s)) (duplicateË¢ â‰ (duplicateË¢ â‰ tailâ–¹Ë¢ s))
      =âŸ¨ ap (consË¢ (consË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s))) (â–¹-ext Î» Î± â†’ ihâ–¹ Î± (tailâ–¹Ë¢ s Î±) âˆ™ ap (Î» q â†’ mapË¢ q (duplicateË¢ (tailâ–¹Ë¢ s Î±))) (fix-path duplicateË¢-body)) âŸ©
    consË¢ (consË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s)) (mapË¢ (duplicateË¢-body (next duplicateË¢)) â‰ (duplicateË¢ â‰ tailâ–¹Ë¢ s))
      =âŸ¨ mapË¢-eq (duplicateË¢-body (next duplicateË¢)) s (duplicateË¢ â‰ tailâ–¹Ë¢ s) âŸ¨
    mapË¢ (duplicateË¢-body (next duplicateË¢)) (consË¢ s (duplicateË¢ â‰ tailâ–¹Ë¢ s))
      =âŸ¨âŸ©
    mapË¢ (duplicateË¢-body (next duplicateË¢)) (duplicateË¢-body (next duplicateË¢) s)
      =âŸ¨ ap (Î» q â†’ mapË¢ q (duplicateË¢-body (next duplicateË¢) s)) (fix-path duplicateË¢-body) âŸ¨
    mapË¢ duplicateË¢ (duplicateË¢-body (next duplicateË¢) s)
      =âŸ¨ ap (Î» q â†’ mapË¢ duplicateË¢ (q s)) (fix-path duplicateË¢-body) âŸ¨
    mapË¢ duplicateË¢ (duplicateË¢ s)
      âˆ

-- natural numbers

natsË¢-body : â–¹ Stream â„• â†’ Stream â„•
natsË¢-body natsâ–¹ = consË¢ 0 ((mapË¢ suc) â‰ natsâ–¹)

natsË¢ : Stream â„•
natsË¢ = fix natsË¢-body

natsË¢-tail : tailâ–¹Ë¢ natsË¢ ï¼ next (mapË¢ suc natsË¢)
natsË¢-tail =
  ap tailâ–¹Ë¢ (fix-path natsË¢-body)
  âˆ™ tail-cons 0 (Î» Î± â†’ mapË¢ suc (next (fix natsË¢-body) Î±))

-- Fibonacci numbers

fibË¢-body : â–¹ Stream â„• â†’ Stream â„•
fibË¢-body fibâ–¹ = consË¢ 0 ((Î» s â†’ consË¢ 1 ((zipWithË¢ _+_ s) â‰ (tailâ–¹Ë¢ s))) â‰ fibâ–¹)

fibË¢ : Stream â„•
fibË¢ = fix fibË¢-body

-- prime numbers

primesË¢-body : â–¹ Stream â„• â†’ Stream â„•
primesË¢-body prâ–¹ = consË¢ 2 ((mapË¢ suc âˆ˜ scanl1Ë¢ _Â·_) â‰ prâ–¹)

primesË¢ : Stream â„•
primesË¢ = fix primesË¢-body

-- paperfolding / dragon curve sequence

toggleË¢ : Stream Bool
toggleË¢ = fix Î» tâ–¹ â†’ consË¢ true (next (consË¢ false tâ–¹))

paperfoldsË¢ : Stream Bool
paperfoldsË¢ = fix (interleaveË¢ toggleË¢)

-- Thue-Morse sequence

hË¢-body : â–¹ (Stream Bool â†’ Stream Bool) â†’ Stream Bool â†’ Stream Bool
hË¢-body hâ–¹ s with (headË¢ s)
... | false = consË¢ false (next (consË¢ true  (hâ–¹ âŠ› tailâ–¹Ë¢ s)))
... | true  = consË¢ true  (next (consË¢ false (hâ–¹ âŠ› tailâ–¹Ë¢ s)))

hË¢ : Stream Bool â†’ Stream Bool
hË¢ = fix hË¢-body

thuemorseË¢ : Stream Bool
thuemorseË¢ = fix Î» tâ–¹ â†’ consË¢ false ((Î» tm â†’ consË¢ true (hË¢ â‰ (tailâ–¹Ë¢ (hË¢ tm)))) â‰ tâ–¹)

-- Pascal coefficients

pascal-nextË¢ : Stream â„• â†’ Stream â„•
pascal-nextË¢ xs = fix Î» pâ–¹ â†’ consË¢ 1 ((zipWithË¢ _+_) â‰ (tailâ–¹Ë¢ xs) âŠ› pâ–¹)

pascalË¢ : Stream (Stream â„•)
pascalË¢ = fix Î» pâ–¹ â†’ consË¢ (repeatË¢ 1) ((mapË¢ pascal-nextË¢) â‰ pâ–¹)
