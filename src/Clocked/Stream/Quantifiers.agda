{-# OPTIONS --guarded #-}
module Clocked.Stream.Quantifiers where

open import Prelude
open import Data.Empty

open import Later
open import Clocked.Stream

private variable
  â„“â‚ â„“â‚‚ â„“â‚ƒ â„“â€² â„“â€³ â„“â€´ : Level
  A : ğ’° â„“â‚
  B : ğ’° â„“â‚‚
  C : ğ’° â„“â‚ƒ
  k : Cl

-- predicates on a stream

-- indexed existential

data gAt (k : Cl) (P : A â†’ ğ’° â„“â€²) : â„• â†’ gStream k A â†’ ğ’° (level-of-type A âŠ” â„“â€²) where
  gAt-here  : âˆ€ {a sâ–¹}
            â†’ P a â†’ gAt k P 0 (cons a sâ–¹)
  gAt-there : âˆ€ {a sâ–¹ n}
            â†’ â–¹[ Î± âˆ¶ k ] (gAt k P n (sâ–¹ Î±))
            â†’ gAt k P (suc n) (cons a sâ–¹)

At : (A â†’ ğ’° â„“â€²) â†’ â„• â†’ Stream A â†’ ğ’° (level-of-type A âŠ” â„“â€²)
At P n s = âˆ€ k â†’ gAt k P n (s k)

At-here : {P : A â†’ ğ’°} {a : A} {s : Stream A}
        â†’ P a â†’ At P 0 (consË¢ a s)
At-here pa k = gAt-here pa

At-there : {P : A â†’ ğ’°} {n : â„•} {a : A} {s : Stream A}
         â†’ At P n s â†’ At P (suc n) (consË¢ a s)
At-there at k = gAt-there (next (at k))

At-tail : {P : A â†’ ğ’°} {n : â„•} {s : Stream A}
        â†’ At P (suc n) s â†’ At P n (tailË¢ s)
At-tail {P} {n} {s} a = force go
  where
  go : âˆ€ Îº â†’ â–¹ Îº (gAt Îº P n (tailË¢ s Îº))
  go Îº with s Îº | recall s Îº | a Îº
  ... | cons h tâ–¹ | âŸª e âŸ« | gAt-there aâ–¹ =
    Î» Î± â†’ subst (gAt Îº P n) (sym ((force-delay (tailâ–¹áµ âˆ˜ s) Îº Î±) âˆ™ Î» i â†’ tailâ–¹áµ (e i) Î±)) (aâ–¹ Î±)


gAt-map : {P : A â†’ ğ’°} {Q : B â†’ ğ’°} {f : A â†’ B}
        â†’ (âˆ€ {x} â†’ P x â†’ Q (f x))
        â†’ (n : â„•) â†’ (s : gStream k A)
        â†’ gAt k P n s â†’ gAt k Q n (mapáµ f s)
gAt-map {k} {Q} {f} pq =
  fix Î» prfâ–¹ â†’ Î» where
    .zero    .(cons a sâ–¹) (gAt-here {a} {sâ–¹} p)   â†’ gAt-here (pq p)
    .(suc n) .(cons a sâ–¹) (gAt-there {a} {sâ–¹} {n} aâ–¹) â†’
       subst (gAt k Q (suc n)) (sym $ mapáµ-eq f a sâ–¹) $
       gAt-there {a = f a} (prfâ–¹ âŠ› next n âŠ›â–¹ sâ–¹ âŠ›â–¹ aâ–¹)

At-map : {P : A â†’ ğ’°} {Q : B â†’ ğ’°} {f : A â†’ B}
       â†’ (âˆ€ {x} â†’ P x â†’ Q (f x))
       â†’ (n : â„•) â†’ (s : Stream A)
       â†’ At P n s â†’ At Q n (mapË¢ f s)
At-map pq n s a k = gAt-map pq n (s k) (a k)

-- universal

data gAll (k : Cl) (P : A â†’ ğ’° â„“â€²) : gStream k A â†’ ğ’° (level-of-type A âŠ” â„“â€²) where
  gAll-cons : âˆ€ {a sâ–¹}
            â†’ P a â†’ â–¹[ Î± âˆ¶ k ] (gAll k P (sâ–¹ Î±))
            â†’ gAll k P (cons a sâ–¹)

gAll-repeat : {P : A â†’ ğ’° â„“â€²}
            â†’ âˆ€ a â†’ P a â†’ gAll k P (repeatáµ a)
gAll-repeat {k} {P} a Pa =
  fix {k = k} Î» ihâ–¹ â†’
    gAll-cons Pa Î» Î± â†’ transport (Î» i â†’ gAll k P (pfix (cons a) (~ i) Î±)) (ihâ–¹ Î±)

gAll-map : {P : A â†’ ğ’° â„“â€²} {Q : B â†’ ğ’° â„“â€³} {f : A â†’ B}
          â†’ ({x : A} â†’ P x â†’ Q (f x))
          â†’ (s : gStream k A) â†’ gAll k P s â†’ gAll k Q (mapáµ f s)
gAll-map {k} {Q} {f} pq =
  fix {k = k} Î» prfâ–¹ â†’ Î» where
    .(cons a sâ–¹) (gAll-cons {a} {sâ–¹} pa pasâ–¹) â†’
       subst (gAll k Q) (sym $ mapáµ-eq f a sâ–¹) $
       gAll-cons (pq pa) (Î» Î± â†’ prfâ–¹ Î± (sâ–¹ Î±) (pasâ–¹ Î±))

gAll-zipWith : {P : A â†’ ğ’° â„“â€²} {Q : B â†’ ğ’° â„“â€³} {R : C â†’ ğ’° â„“â€´} {f : A â†’ B â†’ C}
             â†’ (âˆ€ {x y} â†’ P x â†’ Q y â†’ R (f x y))
             â†’ (s : gStream k A) â†’ (t : gStream k B)
             â†’ gAll k P s â†’ gAll k Q t â†’ gAll k R (zipWitháµ f s t)
gAll-zipWith {k} {R} {f} pqr = fix Î» prfâ–¹ â†’ Î» where
  .(cons a sâ–¹) .(cons b tâ–¹) (gAll-cons {a} {sâ–¹} pa asâ–¹) (gAll-cons {a = b} {sâ–¹ = tâ–¹} qb atâ–¹) â†’
     subst (gAll k R) (sym $ zipWitháµ-eq f a sâ–¹ b tâ–¹) $
     gAll-cons (pqr pa qb) Î» Î± â†’ prfâ–¹ Î± (sâ–¹ Î±) (tâ–¹ Î±) (asâ–¹ Î±) (atâ–¹ Î±)

All : (A â†’ ğ’° â„“â€²) â†’ Stream A â†’ ğ’° (level-of-type A âŠ” â„“â€²)
All P s = âˆ€ k â†’ gAll k P (s k)

All-repeat : {P : A â†’ ğ’° â„“â€²}
           â†’ âˆ€ a â†’ P a â†’ All P (repeatË¢ a)
All-repeat a Pa k = gAll-repeat a Pa

All-map : {P : A â†’ ğ’° â„“â€²} {Q : B â†’ ğ’° â„“â€³} {f : A â†’ B}
         â†’ ({x : A} â†’ P x â†’ Q (f x))
         â†’ (s : Stream A) â†’ All P s â†’ All Q (mapË¢ f s)
All-map pq s ps k = gAll-map pq (s k) (ps k)

All-zipWith : {P : A â†’ ğ’° â„“â€²} {Q : B â†’ ğ’° â„“â€³} {R : C â†’ ğ’° â„“â€´} {f : A â†’ B â†’ C}
            â†’ (âˆ€ {x y} â†’ P x â†’ Q y â†’ R (f x y))
            â†’ (s : Stream A) â†’ (t : Stream B)
            â†’ All P s â†’ All Q t â†’ All R (zipWithË¢ f s t)
All-zipWith pqr s t ps pt k = gAll-zipWith pqr (s k) (t k) (ps k) (pt k)

-- prefix versions

data gAnyâ‰¤ (k : Cl) (P : A â†’ ğ’° â„“â€²) : â„• â†’ gStream k A â†’ ğ’° (level-of-type A âŠ” â„“â€²) where
  gAnyâ‰¤-here  : âˆ€ {a sâ–¹ n}
              â†’ P a â†’ gAnyâ‰¤ k P n (cons a sâ–¹)
  gAnyâ‰¤-there : âˆ€ {a sâ–¹ n}
              â†’ â–¹[ Î± âˆ¶ k ] (gAnyâ‰¤ k P n (sâ–¹ Î±))
              â†’ gAnyâ‰¤ k P (suc n) (cons a sâ–¹)

gAnyâ‰¤-map : {P : A â†’ ğ’°} {Q : B â†’ ğ’°} {f : A â†’ B}
          â†’ (âˆ€ {x} â†’ P x â†’ Q (f x))
          â†’ (n : â„•) â†’ (s : gStream k A)
          â†’ gAnyâ‰¤ k P n s â†’ gAnyâ‰¤ k Q n (mapáµ f s)
gAnyâ‰¤-map {k} {Q} {f} pq =
  fix Î» prfâ–¹ â†’ Î» where
    n        .(cons a sâ–¹) (gAnyâ‰¤-here {a} {sâ–¹} pa)      â†’ gAnyâ‰¤-here (pq pa)
    .(suc n) .(cons a sâ–¹) (gAnyâ‰¤-there {a} {sâ–¹} {n} aâ–¹) â†’
       subst (gAnyâ‰¤ k Q (suc n)) (sym $ mapáµ-eq f a sâ–¹) $
       gAnyâ‰¤-there (prfâ–¹ âŠ› (next n) âŠ›â–¹ sâ–¹ âŠ›â–¹ aâ–¹)

Anyâ‰¤ : (A â†’ ğ’° â„“â€²) â†’ â„• â†’ Stream A â†’ ğ’° (level-of-type A âŠ” â„“â€²)
Anyâ‰¤ P n s = âˆ€ k â†’ gAnyâ‰¤ k P n (s k)

Anyâ‰¤-map : {P : A â†’ ğ’°} {Q : B â†’ ğ’°} {f : A â†’ B}
         â†’ (âˆ€ {x} â†’ P x â†’ Q (f x))
         â†’ (n : â„•) â†’ (s : Stream A)
         â†’ Anyâ‰¤ P n s â†’ Anyâ‰¤ Q n (mapË¢ f s)
Anyâ‰¤-map pq n s a k = gAnyâ‰¤-map pq n (s k) (a k)

data gAllâ‰¤ (k : Cl) (P : A â†’ ğ’° â„“â€²) : â„• â†’ gStream k A â†’ ğ’° (level-of-type A âŠ” â„“â€²) where
  gAllâ‰¤-nil  : âˆ€ {a sâ–¹}
             â†’ P a
             â†’ gAllâ‰¤ k P zero (cons a sâ–¹)
  gAllâ‰¤-cons : âˆ€ {a sâ–¹ n}
             â†’ P a â†’ â–¹[ Î± âˆ¶ k ] (gAllâ‰¤ k P n (sâ–¹ Î±))
             â†’ gAllâ‰¤ k P (suc n) (cons a sâ–¹)

Allâ‰¤ : (A â†’ ğ’° â„“â€²) â†’ â„• â†’ Stream A â†’ ğ’° (level-of-type A âŠ” â„“â€²)
Allâ‰¤ P n s = âˆ€ k â†’ gAllâ‰¤ k P n (s k)

Allâ‰¤-nil : âˆ€ {P : A â†’ ğ’° â„“â€²} {a s}
         â†’ P a â†’ Allâ‰¤ P zero (consË¢ a s)
Allâ‰¤-nil p k = gAllâ‰¤-nil p

Allâ‰¤-cons : âˆ€ {P : A â†’ ğ’° â„“â€²} {a s n}
          â†’ P a â†’ Allâ‰¤ P n s  -- guard?
          â†’ Allâ‰¤ P (suc n) (consË¢ a s)
Allâ‰¤-cons p a k = gAllâ‰¤-cons p (next (a k))

-- adjacent elements

data gAdj (k : Cl) (P : A â†’ A â†’ ğ’° â„“â€²) : gStream k A â†’ ğ’° (level-of-type A âŠ” â„“â€²) where
  gAdj-cons : âˆ€ {a sâ–¹}
            â†’ â–¹[ Î± âˆ¶ k ] P a (headáµ (sâ–¹ Î±))
            â†’ â–¹[ Î± âˆ¶ k ] (gAdj k P (sâ–¹ Î±))
            â†’ gAdj k P (cons a sâ–¹)

repeat-gadj : {P : A â†’ A â†’ ğ’° â„“â€²}
            â†’ âˆ€ a â†’ P a a â†’ gAdj k P (repeatáµ a)
repeat-gadj {k} {P} a Pr =
  fix Î» ihâ–¹ â†’ gAdj-cons (Î» Î± â†’ transport (Î» i â†’ P a (headáµ (pfix (cons a) (~ i) Î±))) Pr)
                        (Î» Î± â†’ transport (Î» i â†’ gAdj k P (pfix (cons a) (~ i) Î±)) (ihâ–¹ Î±))

Adj : (A â†’ A â†’ ğ’° â„“â€²) â†’ Stream A â†’ ğ’° (level-of-type A âŠ” â„“â€²)
Adj P s = âˆ€ k â†’ gAdj k P (s k)

repeat-adj : {P : A â†’ A â†’ ğ’° â„“â€²}
           â†’ âˆ€ a â†’ P a a â†’ Adj P (repeatË¢ a)
repeat-adj a Pr k = repeat-gadj a Pr
